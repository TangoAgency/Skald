// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->

  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    maven { url 'https://jitpack.io' }
  }

  apply plugin: 'checkstyle'

  checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    toolVersion = "7.3"
  }

  task checkstyle(type: Checkstyle) {}

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      jcenter()
      mavenCentral()
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:2.3.3'
      classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
      classpath 'com.novoda:bintray-release:0.3.4'
      classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
      classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
  }
}

allprojects {
  repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    flatDir {
      dirs "libs"
    }
    mavenCentral()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  minSdkVersion = 15
  targetSdkVersion = 25
  compileSdkVersion = 25
  buildToolsVersion = '25.0.2'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

def androidToolsVersion = '25.2.0'
def supportLibraryVersion = '25.1.1'
def googlePlayServicesVersion = '10.0.1'
ext.deps = [// Android
            android               : 'com.google.android:android:4.1.1.4',
            supportCompat         : "com.android.support:support-compat:$supportLibraryVersion",
            supportFragment       : "com.android.support:support-fragment:$supportLibraryVersion",
            appCompat             : "com.android.support:appcompat-v7:$supportLibraryVersion",
            supportAnnotations    : "com.android.support:support-annotations:$supportLibraryVersion",
            supportTestRunner     : 'com.android.support.test:runner:0.5',
            androidGradle         : 'com.android.tools.build:gradle:2.2.0',

            lint                  : "com.android.tools.lint:lint:$androidToolsVersion",
            lintApi               : "com.android.tools.lint:lint-api:$androidToolsVersion",
            lintChecks            : "com.android.tools.lint:lint-checks:$androidToolsVersion",
            lintTests             : "com.android.tools.lint:lint-tests:$androidToolsVersion",

            //Spotify
            spotifyAuth           : 'com.spotify.android:auth:1.0.0-alpha',
            spotifyPlayer         : 'com.spotify.sdk:spotify-player-24-noconnect-2.20b@aar',

            //Deezer
            deezer                : 'com.deezer:deezer-sdk-0.11.2@aar',

            // Google Services
            googlePlayServicesMaps: "com.google.android.gms:play-services-maps:$googlePlayServicesVersion",
            googleMapsUtils       : "com.google.maps.android:android-maps-utils:0.4.4",

            //dagger
            dagger                : 'com.google.dagger:dagger:2.9',
            daggerCompiler        : 'com.google.dagger:dagger-compiler:2.9',

            //RxJava
            rxJava                : 'io.reactivex.rxjava2:rxjava:2.0.0',
            rxAndroid             : 'io.reactivex.rxjava2:rxandroid:2.0.0',

            // Square
            javapoet              : 'com.squareup:javapoet:1.7.0',
            rxJavaAdapter         : 'com.squareup.retrofit2:adapter-rxjava2:2.3.0',

            // Third Party
            javaparser            : 'com.github.javaparser:javaparser-core:2.4.0',
            javax                 : 'javax.inject:javax.inject:1',
            gson                  : "com.squareup.retrofit2:converter-gson:2.3.0",

            // Test dependencies
            junit                 : 'junit:junit:4.12',
            mockito               : 'org.mockito:mockito-core:2.7.5',
            truth                 : 'com.google.truth:truth:0.28',
            robolectric           : 'org.robolectric:robolectric:3.2.2',
            compiletesting        : 'com.google.testing.compile:compile-testing:0.9',
            autoservice           : 'com.google.auto.service:auto-service:1.0-rc2',
            autocommon            : 'com.google.auto:auto-common:0.6',]